buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins{
    id 'com.matthewprenger.cursegradle' version '1.0.10'
}
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = project.version
group = "pro.kdray.funniray" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "mixer-forge"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.2.2611"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20171003"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

dependencies {
    compile project(':mixer-common')
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

shadowJar {
    dependencies {
        include(dependency(':mixer-common'))
        include(dependency("com.mixer:api:${gradle.mixerAPI}"))
        include(dependency("com.mixer:mixer-interactive-api:${gradle.mixerInteractiveAPI}"))
        include(dependency("org.apache.httpcomponents:httpclient:${gradle.httpclient}"))
        include(dependency("org.apache.httpcomponents:httpcore:${gradle.httpcore}"))
        include(dependency("org.java-websocket:Java-WebSocket:${gradle.JavaWebSocket}"))
        include(dependency("com.zaxxer:HikariCP:${gradle.HikariCP}"))
    }
    destinationDir = file("$rootDir/build/jars")
}

curseforge {
    apiKey = project.curseForgeToken
    project {
        id = '291641'
        changelog = "Unstable build, please refer to github for changes."
        releaseType = 'alpha'
        addGameVersion '1.12.2'

        mainArtifact(jar) {
            displayName = archivesBaseName+project.version+".jar"
        }
    }
}
